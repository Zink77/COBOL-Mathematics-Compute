
       IDENTIFICATION DIVISION.
       PROGRAM-ID. CS370-PROGRAM1-JJ.
       AUTHOR.     JORDAN JOHNSON.

      ****************************************************************
      * This is a program produces a TRUCK REPORT RECORD
      * Listing: Truck ID, Item Name, Stock Number, Purchase Price,
      * Total Price of Stock, and cumulative totals
      * ******
      * INPUT:
      *    The TRUCK INVENTORY FILE contains the following
      *    data in each record:
      *         1.  TRUCK ID
      *         2.  EMPLOYEE ID
      *         3.  EMPLOYEE NAME
      *         4.  ITEM ID
      *         5.  NUMBER IN STOCK
      *         6.  ITEM PURCHASE PRICE
      *         7.  ITEM SELLING PRICE
      *
      * *******
      * OUTPUT:
      *    The TRUCK REPORT RECORD contains the following information:
      *    ************
      *    DETAIL LINE:
      *         1.  TRUCK ID
      *         2.  ITEM NAME
      *         3.  NUMBER IN STOCK
      *         4.  ITEM PURCHASE PRICE
      *         5.  TOTAL INVENTORY COST
      *         6.  TOTAL STOCK
      *         7.  TOTAL PURCHASE PRICE
      *         8.  TOTAL INVENTORY SUM PRICE
      *
      * *************
      * CALCULATIONS:
      *    TOTAL INVENTORY COST = PURCHASE PRICE * NUMBER IN STOCK
      *    TOTAL STOCK = NUMBER IN STOCK + STOCK-SUM-HOLDER
      *    TOTAL PURCHASE PRICE = PURCHASE PRICE + PURCHASE-SUM-HOLDER
      *    TOTAL INVENTORY SUM PRICE =
      *                      TOTAL-INVEN-SUM-HOLDER + TOTAL-INVEN-COST
      ****************************************************************

       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER.    IBMPC.
       OBJECT-COMPUTER.    IBMPC.

       INPUT-OUTPUT SECTION.
       FILE-CONTROL.


           SELECT CURRENT-TRUCK-INVENTORY
               ASSIGN TO 'PR2FA22-Inven.txt'
               ORGANIZATION IS LINE SEQUENTIAL.


           SELECT CURRENT-TRUCK-REPORT
               ASSIGN TO PRINTER 'TRUCK-REPORT.TXT'.

      *

       DATA DIVISION.
       FILE SECTION.

      *Prepares the program for what the imput file holds
       FD  CURRENT-TRUCK-INVENTORY
           RECORD CONTAINS 40 CHARACTERS.
       01  TRUCK-REC.
           05  TRUCK-ID             PIC X(5).
           05  EMPLOYEE-ID          PIC X(4).
           05  EMPLOYEE-NAME        PIC X(20).
           05  ITEM-ID              PIC A(2).
           05  STOCK-NUM            PIC 9(3).
           05  PURCHASE-PRICE       PIC 99V99.
           05  SELLING-PRICE        PIC 99V99.
      *
       FD    CURRENT-TRUCK-REPORT
             RECORD CONTAINS 80 CHARACTERS.
       01    TRUCK-REPORT-REC            PIC X(80).


       WORKING-STORAGE SECTION.

       01    WS-WORK-AREAS.
             05    ARE-THERE-MORE-RECORDS    PIC X(3) VALUE 'YES'.

       01 WS-Date.
             05 WS-Run-Year Pic 9999.
             05 WS-Run-Month Pic 99.
             05 WS-Run-Day Pic 99.


      *Uses MATH-HELPERS to hold sums, you cannot use math with
      *editing marks such as decimal points
       01 MATH-HELPERS.
           05 STOCK-SUM-HOLDER        PIC 9999.
           05 PURCHASE-SUM-HOLDER     PIC 999V99.
           05 TOTAL-INVEN-SUM-HOLDER  PIC 9999V99.
           05 TOTAL-INVEN-COST        PIC 999V99.

       01  REPORT-FIELDS.
           05  PROPER-SPACING          PIC S9        VALUE +3.

      *************************OUTPUT AREA*****************************
      *Formats the headers for writing
       01 REPORT-HEADER1.
           05 FILLER          PIC XX    VALUE SPACES.
           05 RH-DATE.
              10 Run-Year     Pic 9999.
              10 FILLER       Pic X Value '/'.
              10 Run-Month    Pic 99.
              10 FILLER       Pic X Value '/'.
              10 Run-Day      Pic 99.
           05 FILLER          PIC X(23) VALUE SPACES.
           05 FILLER          PIC X(13) VALUE 'ROLLING PIZZA'.
           05 FILLER          PIC X(19) VALUE SPACES.
           05 FILLER          PIC X(3) VALUE 'JAJ'.
           05 FILLER          PIC X(9) VALUE SPACES.

       01 REPORT-HEADER2.
           05 FILLER          PIC X(33) VALUE SPACES.
           05 FILLER          PIC X(16) VALUE 'INVENTORY REPORT'.

       01 REPORT-HEADER3.
           05 FILLER          PIC X(5) VALUE SPACES.
           05 FILLER          PIC X(5) VALUE 'TRUCK'.
           05 FILLER          PIC X(11) VALUE SPACES.
           05 FILLER          PIC X(4) VALUE 'ITEM'.
           05 FILLER          PIC X(10) VALUE SPACES.
           05 FILLER          PIC X(6) VALUE 'NUM IN'.
           05 FILLER          PIC X(5) VALUE SPACES.
           05 FILLER          PIC X(8) VALUE 'PURCHASE'.
           05 FILLER          PIC X(5) VALUE SPACES.
           05 FILLER          PIC X(11) VALUE 'TOTAL INVEN'.
           05 FILLER          PIC X(9) VALUE SPACES.

       01 REPORT-HEADER4.
           05 FILLER          PIC X(6) VALUE SPACES.
           05 FILLER          PIC X(2) VALUE 'ID'.
           05 FILLER          PIC X(13) VALUE SPACES.
           05 FILLER          PIC X(4) VALUE 'NAME'.
           05 FILLER          PIC X(10) VALUE SPACES.
           05 FILLER          PIC X(5) VALUE 'STOCK'.
           05 FILLER          PIC X(7) VALUE SPACES.
           05 FILLER          PIC X(5) VALUE 'PRICE'.
           05 FILLER          PIC X(10) VALUE SPACES.
           05 FILLER          PIC X(4) VALUE 'COST'.
           05 FILLER          PIC X(13) VALUE SPACES.

      *Prepares the DETAIL-LINE for the records
       01 DETAIL-LINE.
           05 FILLER             PIC X(5) VALUE SPACES.
           05 DL-TRUCK-ID        PIC X(5).
           05 FILLER             PIC X(5) VALUE SPACES.
           05 DL-ITEM-NAME       PIC X(15).
           05 FILLER             PIC X(7) VALUE SPACES.
           05 DL-STOCK-NUM       PIC ZZ9.
           05 FILLER             PIC X(7) VALUE SPACES.
           05 FILLER             PIC X VALUE '$'.
           05 DL-PURCHASE-PRICE  PIC Z9.99.
           05 FILLER             PIC X(9) VALUE SPACES.
           05 FILLER             PIC X VALUE '$'.
           05 DL-TOTAL-INVEN-COST   PIC ZZ9.99.
           05 FILLER             PIC X(10) VALUE SPACES.

      *Prepares a TOTAL-LINE for the last line of sums
       01 TOTAL-LINE.
           05 FILLER             PIC X(25) VALUE SPACES.
           05 FILLER             PIC X(7) VALUE 'TOTALS:'.
           05 FILLER             PIC X(4) VALUE SPACES.
           05 DL-TOTAL-STOCK     PIC ZZZ9.
           05 FILLER             PIC X(6).
           05 FILLER             PIC X VALUE '$'.
           05 DL-TOTAL-PURCHASE  PIC ZZ9.99.
           05 FILLER             PIC X(7).
           05 FILLER             PIC X VALUE '$'.
           05 DL-SUM-INVEN-COST  PIC Z,ZZ9.99.


       PROCEDURE DIVISION.

       100-MAIN-MODULE.

           PERFORM 125-HOUSEKEEPING
           PERFORM 145-HEADER-DIVISION
           PERFORM 150-READ-RECORDS
           PERFORM 250-CLOSE-ROUTINE

           .

      *Sets files and date
       125-HOUSEKEEPING.


           OPEN    INPUT     CURRENT-TRUCK-INVENTORY
                   OUTPUT    CURRENT-TRUCK-REPORT

           Move Function Current-Date To WS-Date
                   MOVE WS-RUN-YEAR      TO RUN-YEAR
                   MOVE WS-RUN-MONTH     TO RUN-MONTH
                   MOVE WS-RUN-DAY       TO RUN-DAY

           .
      *Prints header lines to the output
       145-HEADER-DIVISION.
           MOVE REPORT-HEADER1 TO TRUCK-REPORT-REC
           MOVE 1 TO PROPER-SPACING
           WRITE TRUCK-REPORT-REC
                   AFTER ADVANCING PROPER-SPACING

           MOVE REPORT-HEADER2 TO TRUCK-REPORT-REC
           MOVE 2 TO PROPER-SPACING
           WRITE TRUCK-REPORT-REC
                   AFTER ADVANCING PROPER-SPACING

           MOVE REPORT-HEADER3 TO TRUCK-REPORT-REC
           MOVE 3 TO PROPER-SPACING
           WRITE TRUCK-REPORT-REC
                   AFTER ADVANCING PROPER-SPACING

           MOVE REPORT-HEADER4 TO TRUCK-REPORT-REC
           MOVE 1 TO PROPER-SPACING
           WRITE TRUCK-REPORT-REC
                   AFTER ADVANCING PROPER-SPACING

           Move 2 to PROPER-SPACING

           .
      *Reads in each record, while there are still records
       150-READ-RECORDS.

             PERFORM UNTIL ARE-THERE-MORE-RECORDS = 'NO'
                  READ CURRENT-TRUCK-INVENTORY
                      AT END
                          MOVE 'NO' TO ARE-THERE-MORE-RECORDS
                      NOT AT END
                          PERFORM 175-PROCESS-VALIDATION
                  END-READ
              END-PERFORM

              MOVE 2 TO PROPER-SPACING

              PERFORM 205-FINAL-TOTALS

           .

      *Validates the data:
      *Is the truck ID valid? If not, print ERROR
      *Are the numeric values numeric? If not, replace with 0s
       175-PROCESS-VALIDATION.

              IF TRUCK-ID = 'P100A'
                  MOVE TRUCK-ID TO DL-TRUCK-ID
              ELSE
                  IF TRUCK-ID = 'P200G'
                    MOVE TRUCK-ID TO DL-TRUCK-ID
                  ELSE
                      IF TRUCK-ID = 'P300H'
                        MOVE TRUCK-ID TO DL-TRUCK-ID
                      ELSE
                          MOVE 'ERROR' TO DL-TRUCK-ID
                      END-IF
                   END-IF
               END-IF


              IF STOCK-NUM IS NUMERIC
                  MOVE STOCK-NUM TO DL-STOCK-NUM
              ELSE
                  MOVE '000' TO STOCK-NUM
              END-IF

              IF PURCHASE-PRICE IS NUMERIC
                  MOVE PURCHASE-PRICE TO DL-PURCHASE-PRICE
              ELSE
                  MOVE '00.00' TO PURCHASE-PRICE
              END-IF

              IF SELLING-PRICE IS NOT NUMERIC
                  MOVE '00.00' TO SELLING-PRICE
              END-IF


              PERFORM 180-ITEM-MATCHUP

           .

      *Matches the Item ID with the correct item
      *Moves the Item Name to the DETAIL-LINE
       180-ITEM-MATCHUP.


              IF ITEM-ID = 'CH'
                  MOVE 'CHEESE PIZZA' TO DL-ITEM-NAME
              END-IF

              IF ITEM-ID = 'PP'
                  MOVE 'PEPPERONI PIZZA' TO DL-ITEM-NAME
              END-IF

              IF ITEM-ID = 'SA'
                  MOVE 'SAUSAGE PIZZA' TO DL-ITEM-NAME
              END-IF

              IF ITEM-ID = 'SU'
                  MOVE 'SUPREME PIZZA' TO DL-ITEM-NAME
              END-IF

              IF ITEM-ID = 'PR'
                  MOVE 'PRETZEL' TO DL-ITEM-NAME
              END-IF

           PERFORM 185-CALCULATIONS


           .

      *Uses the MATH-HELPERS to calculate totals to be printed
       185-CALCULATIONS.

           COMPUTE TOTAL-INVEN-COST = PURCHASE-PRICE * STOCK-NUM

           COMPUTE STOCK-SUM-HOLDER =  STOCK-SUM-HOLDER + STOCK-NUM
           COMPUTE PURCHASE-SUM-HOLDER =
                                 PURCHASE-SUM-HOLDER + PURCHASE-PRICE
           COMPUTE TOTAL-INVEN-SUM-HOLDER =
                        TOTAL-INVEN-SUM-HOLDER + TOTAL-INVEN-COST

           PERFORM 200-PROCESS-RTN

           .

      *Places the remaining basic values into the DETAIL-LINE
       200-PROCESS-RTN.

              MOVE STOCK-NUM        TO DL-STOCK-NUM
              MOVE PURCHASE-PRICE   TO DL-PURCHASE-PRICE
              MOVE TOTAL-INVEN-COST TO DL-TOTAL-INVEN-COST

              MOVE DETAIL-LINE TO TRUCK-REPORT-REC

              WRITE TRUCK-REPORT-REC
                     AFTER ADVANCING PROPER-SPACING

              MOVE 1 TO PROPER-SPACING

           .
      *After all records are read and calculations are complete
      *Places sums into correctly formated total variables
      *Writes variables to the output
       205-FINAL-TOTALS.

              MOVE STOCK-SUM-HOLDER TO DL-TOTAL-STOCK
              MOVE PURCHASE-SUM-HOLDER TO DL-TOTAL-PURCHASE
              MOVE TOTAL-INVEN-SUM-HOLDER TO DL-SUM-INVEN-COST

              MOVE TOTAL-LINE TO TRUCK-REPORT-REC

              WRITE TRUCK-REPORT-REC
                     AFTER ADVANCING PROPER-SPACING

           .

       250-CLOSE-ROUTINE.

           CLOSE CURRENT-TRUCK-REPORT
                 CURRENT-TRUCK-INVENTORY

           STOP RUN.
